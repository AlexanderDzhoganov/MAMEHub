apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'

group = 'MAMEHubClient'
version = '0.0.1-SNAPSHOT'

description = """MAMEHub Client"""

sourceCompatibility = 1.7
targetCompatibility = 1.7

eclipse { classpath { downloadSources=true } }

task generateThrift << {
  new File("src/gen").mkdirs()
  delete "src/gen/gen-java"
  ext.thriftFiles = fileTree(dir: 'src/main/resources/thrift').matching { include '**/*.thrift' }
  for(String thriftFile : ext.thriftFiles) {
    exec {
      executable = 'thrift'
      args = [
        '--gen',
        'java',
        '-o',
        'src/gen/',
        thriftFile
      ]
    }
  }
}

compileJava {
  it.dependsOn generateThrift
}
tasks.eclipseProject.dependsOn(generateThrift)

sourceSets {
  main {
    java {
      srcDir 'src/gen/gen-java'
      srcDir 'src/main/java'
    }
  }
}

repositories {

     maven { url "http://teleal.org/m2" }
     maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version:'1.6.6'
    compile group: 'org.slf4j', name: 'slf4j-log4j12', version:'1.6.6'
    compile group: 'log4j', name: 'log4j', version:'1.2.16'
    compile group: 'org.apache.thrift', name: 'libthrift', version:'0.9.1'
    compile group: 'org.mapdb', name: 'mapdb', version:'1.0.5'
    
    compile group: 'commons-validator', name: 'commons-validator', version:'1.4.0'
    compile group: 'commons-io', name: 'commons-io', version:'2.4'
    compile group: 'commons-cli', name: 'commons-cli', version:'1.2'
    compile group: 'org.apache.commons', name: 'commons-compress', version:'1.6'
    compile group: 'commons-lang', name: 'commons-lang', version:'2.6'
    compile 'commons-configuration:commons-configuration:1.10'
    
    compile group: 'com.jgoodies', name: 'forms', version:'1.2.1'
    compile group: 'org.fourthline.cling', name: 'cling-core', version:'2.0-SNAPSHOT'
    compile group: 'org.fourthline.cling', name: 'cling-support', version:'2.0-SNAPSHOT'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.2.1'
    compile group: 'org.apache.httpcomponents', name: 'httpcore', version:'4.2.1'
    compile group: 'org.lwjgl.lwjgl', name: 'lwjgl', version:'2.8.4'
    compile group: 'org.eclipse.jetty', name: 'jetty-server', version:'8.1.0.RC5'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlets', version:'8.1.0.RC5'
    compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version:'8.1.0.RC5'
    compile group: 'org.apache.lucene', name: 'lucene-core', version:'4.0.0'
    compile group: 'org.apache.lucene', name: 'lucene-analyzers-common', version:'4.0.0'
    compile group: 'org.apache.lucene', name: 'lucene-queryparser', version:'4.0.0'
    compile group: 'org.markdownj', name: 'markdownj', version:'0.3.0-1.0.2b4'
    compile group: 'com.google.code.gson', name: 'gson', version:'2.2.4'
    compile group: 'joda-time', name: 'joda-time', version:'2.2'
    testCompile group: 'junit', name: 'junit', version:'4.10'
}

task solid(type:Jar) {
  manifest {
    attributes 'Main-Class': 'com.mamehub.client.MAMEHub'
  }
  // Build jar with dependencies
  from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
    //exclude "META-INF/*.SF"
    //exclude "META-INF/*.DSA"
    //exclude "META-INF/*.RSA"
    //exclude "META-INF/license"
    //exclude "META-INF/LICENSE"
  }
  baseName = 'MAMEHubClient'
  destinationDir = new File("solid")
  zip64 = true
  with jar
}
